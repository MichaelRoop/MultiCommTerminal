/*
  Name:    DemoArduinoBLE.ino
  Created: Jan 15, 2021

  Sets up the BLE to have characteristics that change value in real time.
	Battery Level	// 0x180F Defined in BLE Spec
	Temperature		// 0x2A6E
	Humidity		// 0x2A6F
	Pressure		// 0x2A6D
	Custom          // My Uuid 0xF001 - has format descriptor for parsing

	Here is a partial list of BLE Format data types for the Format Descriptor. Some are 
	not yet supported. Those will be displayed as a series of bytes
        Boolean = 1
        UInt_2bit = 2
        UInt_4bit = 3
        UInt_8bit = 4
        UInt_12bit = 5
        UInt_16bit = 6
        UInt_24bit = 7
        UInt_32bit = 8
        UInt_48bit = 9
        UInt_64bit = 10
        UInt_128bit = 11 ** Not supported **
        Int_8bit = 12
        Int_12bit = 13  ** Not supported **
        Int_16bit = 14
        Int_24bit = 15  ** Not supported **
        Int_32bit = 16
        Int_48bit = 17 ** Not supported **
        Int_64bit = 18
        Int_128bit = 19 ** Not supported **
        IEEE_754_32bit_floating_point = 20
        IEEE_754_64bit_floating_point = 21
        IEEE_11073_16bit_SFLOAT = 22 ** Not supported **
        IEEE_11073_32bit_FLOAT = 23  ** Not supported **
        IEEE_20601_format = 24  ** Not supported **
        UTF8_String = 25
        UTF16_String = 26
		OpaqueStructure = 27  ** Not supported **


  For this to work you need the forked version of ArduinoBLE
  which supports connection to Windows and Android
  https://github.com/unknownconstant/ArduinoBLE/tree/pairing


  Tested on Arduino Uno WIFI Rev 2

  Arduino code partialy based on:
  https://programmaticponderings.com/2020/08/04/getting-started-with-bluetooth-low-energy-ble-and-generic-attribute-profile-gatt-specification-for-iot/

  With some modifications. 
  Values are generated internally for demo instead or reading I/O
  
 */
#include <ArduinoBLE.h>

//---------------------------------------------------------------------
// Variables
const int GENERIC_ACCESS_APPEARANCE_ID = 1792; // BLE Spec 0x1792

// Create battery service
BLEService batteryService("180F");
BLETypedCharacteristic<uint8_t> batteryLevelCharacteristic("2A19", BLERead | BLENotify);

// Environment sensing service
BLEService environmentService("181A");
BLETypedCharacteristic<uint32_t> pressureCharacteristic("2A6D", BLERead | BLENotify);
BLETypedCharacteristic<int16_t> tempCharacteristic("2A6E", BLERead | BLENotify);
BLETypedCharacteristic<uint16_t> humidCharacteristic("2A6F", BLERead | BLENotify);

// Custom 2 byte sensor service
BLEService ioService("FF01");
BLETypedCharacteristic<uint16_t> analogSensorCharacteristic("F001", BLERead | BLENotify);
BLEDescriptor sensorDescriptorUserDescription("2901", "aSensor1"); // Defined in Spec
uint8_t format[7] = { 
	0x6,		// Data type. BLE spec 6 = uint16_t. Defines size of data in characteristic read 
	(byte)-2,	// Exponent -2 sbyte. stored as byte. parsed out as sbyte to maintain sign
	0x0, 0x0 ,	// Measurement uint16_t. 0 is unit-less. Adds abbreviation on display string
	0x0,		// Namespace byte. 0 is undefined, 1 is BLE Sig (only one defined in spec)
	0x0, 0x0	// Description uint
};
// BLE spec 0x2904 is the Format Descriptor
BLEDescriptor senforDescriptorFormat("2904", format, sizeof(format));

// Track dummy values 
uint8_t batteryLevel = 93;
int16_t tempLevel = 3392;
uint16_t humidityLevl = 4400;
uint32_t pressureLevel = 28500;
uint16_t analogLevel = 16025;

unsigned long lasMsTimeUpdate = 0;
bool upIncrement = true;
//---------------------------------------------------------------------


//---------------------------------------------------------------------
// Main Arduino functions

// Executed once on power on or reset
void setup() {
	Serial.begin(9600);
	while (!Serial) {}
	Serial.println("started...");
	SetupBLE();
}


// the loop function runs over and over again forever
void loop() {
	BLEDevice central = BLE.central();
	if (central) {
		while (central.connected()) {
			WriteBatteryLevelOnMsInterval(5000);
			delayMicroseconds(10);
		}
	}
}
//---------------------------------------------------------------------

//---------------------------------------------------------------------
// Helper functions
void SetupBLE() {
	if (!BLE.begin()) {
		Serial.println("FAIL");
		while (1);
	}
	// Device
	BLE.setLocalName("Mikie BLE"); // visible in search
	BLE.setDeviceName("MR BLE Device");
	BLE.setAppearance(GENERIC_ACCESS_APPEARANCE_ID);
	BLE.setEventHandler(BLEConnected, bleOnConnectHandler);
	BLE.setEventHandler(BLEDisconnected, bleOnDisconnectHandler);
	
	// Battery service
	batteryService.addCharacteristic(batteryLevelCharacteristic);
	BLE.addService(batteryService);
	BLE.setAdvertisedService(batteryService);

	// Environment sensing service with 3 characteristics
	environmentService.addCharacteristic(tempCharacteristic);
	environmentService.addCharacteristic(humidCharacteristic);
	environmentService.addCharacteristic(pressureCharacteristic);
	BLE.addService(environmentService);
	BLE.setAdvertisedService(environmentService);

	// My custom sensor service
	analogSensorCharacteristic.addDescriptor(sensorDescriptorUserDescription);
	analogSensorCharacteristic.addDescriptor(senforDescriptorFormat);
	ioService.addCharacteristic(analogSensorCharacteristic);
	BLE.addService(ioService);
	BLE.setAdvertisedService(ioService);

	BLE.advertise();

	WriteValues();
	Serial.println("BLE started");
}


void WriteBatteryLevelOnMsInterval(unsigned long msInterval) {
	// Simulate reading of actual hardware sensors for demo
	unsigned long currentMs = millis();
	if ((currentMs - lasMsTimeUpdate) > msInterval) {
		lasMsTimeUpdate = currentMs;
		if (upIncrement) {
			tempLevel += 111;
			humidityLevl += 212;
			pressureLevel += 18;
			analogLevel += 67;
			batteryLevel++;
			if (batteryLevel > 99) {
				batteryLevel = 99;
				upIncrement = false;
				batteryLevel--;
			}
		}
		else {
			tempLevel -= 111;
			humidityLevl -= 212;
			pressureLevel -= 18;
			analogLevel -= 67;
			batteryLevel--;
			if (batteryLevel < 85) {
				upIncrement = true;
				batteryLevel = 60;
			}
		}
		WriteValues();
	}
}


void ResetValues() {
	batteryLevel = 93;
	tempLevel = 3392;
	humidityLevl = 4400;
	pressureLevel = 285;
	analogLevel = 16025;
}


void WriteValues() {
	batteryLevelCharacteristic.writeValue(batteryLevel);
	tempCharacteristic.writeValue(tempLevel);
	humidCharacteristic.writeValue(humidityLevl);
	pressureCharacteristic.writeValue(pressureLevel);
	analogSensorCharacteristic.writeValue(analogLevel);

	Serial.print("    Battery:"); Serial.println(batteryLevel);
	Serial.print("Temperature:"); Serial.println(tempLevel);
	Serial.print("   Pressure:"); Serial.println(pressureLevel);
	Serial.print("   Humidity:"); Serial.println(humidityLevl);
	Serial.print("  An sensor:"); Serial.println(analogLevel);
}

//---------------------------------------------------------------------


//---------------------------------------------------------------------
// Event handlers

// When a connection is established to device
void bleOnConnectHandler(BLEDevice central) {
	Serial.println("CONNECTED");
}


// When a connection to device is broken
void bleOnDisconnectHandler(BLEDevice central) {
	Serial.println("DISCONNECTED");
	ResetValues();
}

//---------------------------------------------------------------------

